#!/bin/csh

if ($#argv < 2 || $#argv > 2) then
  echo
  echo "usage: wdkRelease target_full_path version"
  echo
  exit(-1)
endif

set TARGET_DIR=$1
set VERSION=$2

if (-d "$TARGET_DIR/$VERSION") then
  echo $TARGET_DIR/$VERSION already exists, remove first
  exit(-1)
endif

mkdir -p $TARGET_DIR/$VERSION

cd $TARGET_DIR/$VERSION

echo
echo tagging WDK and WDKToySite ...
svn copy -m "tagging WDK for release $VERSION" \
    https://svn.cbil.upenn.edu/gus/WDK/trunk \
    https://svn.cbil.upenn.edu/gus/WDK/tags/$VERSION
svn copy -m "tagging WDKToySite for release $VERSION" \
    https://svn.cbil.upenn.edu/gus/WDKToySite/trunk \
    https://svn.cbil.upenn.edu/gus/WDKToySite/tags/$VERSION

echo
echo getting WDK and WDKToySite from tagged copy ...
svn co -q https://svn.cbil.upenn.edu/gus/WDK/tags/$VERSION WDK
if ($status != 0) then
  echo could not svn checkout $status
  exit(-1)
endif
svn co -q https://svn.cbil.upenn.edu/gus/WDKToySite/tags/$VERSION WDKToySite
if ($status != 0) then
  echo could not svn checkout $status
  exit(-1)
endif

echo
echo getting install ...
svn co -q https://svn.cbil.upenn.edu/gus/install/trunk install
if ($status != 0) then
  echo could not svn checkout $status
  exit(-1)
endif

echo
echo make a ReleaseNotes.txt file ...
cat WDK/ReleaseNotes.txt WDKToySite/ReleaseNotes.txt >> ReleaseNotes.txt

  
echo 
echo pbckaging things up ...
rm -rf `find . -name '.svn'`
cp ReleaseNotes.txt ..
cd ..
tar -cvzf ${VERSION}.tar.gz $VERSION

rm -rf $VERSION

exit $?
  

